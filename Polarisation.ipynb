{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "4f37e09a",
   "metadata": {},
   "source": [
    "# O-modes in a periodic PIC simulation\n",
    "\n",
    "We need to find the perturbed quantities associated with o-mode waves, \n",
    "\n",
    "The procedure is:\n",
    " - Fix $\\omega$, $\\Omega_s$, $\\Pi_s$ and $\\theta_k$ to find $k_\\parallel$ and $k_\\bot$.\n",
    " - Use the nullspace, or vector associated with the singular value, of the dispersion relation matrix $\\Lambda$ to obtain the electric field polarisation for each of the waves.\n",
    " - Insert the electric field into Faraday's law for the magnetic field polarisation\n",
    " - Obtain the current polarisation from the conductivity tensor, $\\vec J_s = \\sigma_s \\cdot \\vec E$\n",
    " - Obtain the velocity perturbation for each species from their currents.\n",
    " - Obtain the density perturbation for each species from the continuity equation.\n",
    "\n",
    "The conductivity tensor $\\sigma$ is defined for species $s$\n",
    "$$\n",
    "\\sigma_s = \\begin{bmatrix}\n",
    "  \\frac{\\omega}{\\omega^2 - \\Omega_s^2} & \\frac{i \\Omega_s}{\\omega^2 - \\Omega_s^2} & 0\\\\\n",
    "  -\\frac{i \\Omega_s}{\\omega^2 - \\Omega_s^2} & \\frac{\\omega}{\\omega^2 - \\Omega_s^2} & 0\\\\\n",
    "   0 & 0 & \\frac{1}{\\omega} \n",
    "   \\end{bmatrix} i \\epsilon_0  \\Pi_s^2\n",
    "$$\n",
    "\n",
    "The dielectric tensor is then $\\varepsilon = I + \\frac{i}{\\epsilon_0 \\omega} \\sum_s \\sigma_s$, which makes the dispersion relation tensor $\\Lambda = (\\vec{k} \\vec{k}' - I(\\vec k \\cdot\\vec k))\\frac{c^2}{\\omega^2} + \\varepsilon$, for which $\\vec k = |k| [\\sin\\theta, 0, \\cos\\theta]^{T}$ ($\\theta=\\pi/2$ corresponds to perpendicular progation studied here, meaning that $\\vec k$ is aligned along x and $\\vec B$ is aligned along z, even in the EPOCH simulation; $\\theta$ changes from zero then care must be taken to project the polarisation into the coordinate system in which $\\vec k$ remains parallel to $x$ but $\\vec B$ has a nonzero $x$ component).\n",
    "\n",
    "Now the other parameters:\n",
    "\n",
    "Faraday's\n",
    "$$\n",
    "-i\\omega\\vec B_1 = - \\vec k \\times \\vec E_1\n",
    "$$\n",
    "Gauss's law\n",
    "$$\n",
    "i\\vec k \\cdot \\vec E_1 =  \\sum_s\\frac{q_s n_{s,1}}{\\epsilon_0}\\\\\n",
    "$$\n",
    "Ampere's law:\n",
    "$$\n",
    "\\mu_0 \\vec J = i \\vec k \\times \\vec {B}_1 + i \\omega \\mu_0 \\epsilon_0 \\vec E_1\\\\\n",
    "\\sum_s q_s n_{s,0} \\vec {v}_{s,1} = \\frac{i \\vec k \\times \\vec {B}_1}{\\mu_0} + i \\omega \\epsilon_0 \\vec E_1\\\\\n",
    "$$\n",
    "Assume the ions are unperturbed in the initial conditions\n",
    "$$\n",
    "q_e n_{e,0} {v}_{e,1,y} = i \\omega \\epsilon_0 E_{1,x}\\\\\n",
    "q_e n_{e,0} {v}_{e,1,y} = -\\frac{i k_x {B}_{1,z}}{\\mu_0} + i \\omega \\epsilon_0 E_{1,y}\\\\\n",
    "q_e n_{e,0} {v}_{e,1,z} = \\frac{i k_x {B}_{1,y}}{\\mu_0} + i \\omega \\epsilon_0 E_{1,z}\\\\\n",
    "$$\n",
    "\n",
    "$$\n",
    "n_{e,1} = i k_x E_{x,1}\\frac{\\epsilon_0}{q_s}\n",
    "$$\n",
    "\n",
    "Continuity\n",
    "\n",
    "$$\n",
    "\\omega n_{1,s} - n_{0,s} \\vec k_x \\cdot \\vec v_{1,s} = 0\n",
    "$$\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "0f58c4e6",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "fn (generic function with 3 methods)"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using Roots, Plots, LinearAlgebra, Dates\n",
    "\n",
    "const c₀ = 2.99792458e8\n",
    "const ϵ₀ = 8.854187817e-12\n",
    "const μ₀ = 1.25663706144e-6\n",
    "const q₀ = 1.602176487e-19\n",
    "const mₑ₀ = 9.10938188e-31\n",
    "const mi = 1836 * mₑ₀ * 2\n",
    "const memultiplier = 1\n",
    "const mₑ = mₑ₀ * memultiplier\n",
    "const n0 = 1e18\n",
    "const B0 = 2.0\n",
    "fΠ(n, m::Float64=mₑ) = √(q₀^2 * n / m / ϵ₀)\n",
    "const Π0 = fΠ(n0)\n",
    "fΩ(m=mₑ, Z::Int=-1) = Z * q₀ * B0 / m\n",
    "const Ωe = fΩ(mₑ, -1)\n",
    "const Ωi = fΩ(mi, 1)\n",
    "# fωL(n) = sqrt(Ωe^2 / 4 + fΠ(n)^2) + Ωe / 2\n",
    "# const ωL0 = fωL(n0)\n",
    "# fωₕ(n) = √(fΠ(n)^2 + Ωe^2)\n",
    "# const ωₕ0 = √(fΠ(n0)^2 + Ωe^2)\n",
    "const Te_eV = 10e3\n",
    "const Ti_eV = 10e3\n",
    "const vthe = sqrt(2 * Te_eV * q₀ / mₑ)\n",
    "fλD(n) = vthe / fΠ(n)\n",
    "const λD0 = fλD(n0)\n",
    "const fbulkmagneticfieldenergydensity(n) = B0^2 / 2μ₀\n",
    "const θ₀ = 40 * pi / 180\n",
    "# const fbulkenergydensity(n) = fbulkmagneticfieldenergydensity(n) + n * Te_eV * q₀ + n * Ti_eV * q₀\n",
    "\n",
    "function cleaner(z)\n",
    "  nz = norm(z)\n",
    "  normz = z ./ nz\n",
    "  normz = trunc.(real.(normz), digits=16) .+ im * trunc.(imag.(normz), digits=16)\n",
    "  output = normz .* nz\n",
    "  @assert output ≈ z\n",
    "  return output\n",
    "end\n",
    "\n",
    "function fσ(ω, n, m=mₑ, Z=-1)\n",
    "  Ωs = fΩ(m, Z) \n",
    "  Πs = fΠ(n, m)\n",
    "  ω²_Ωs² = (ω^2 - Ωs^2)\n",
    "  return [ω / ω²_Ωs²         im * Ωs / ω²_Ωs²  0.0  ; \n",
    "          -im * Ωs / ω²_Ωs²  ω / ω²_Ωs²        0.0  ;\n",
    "          0.0                0.0               1 / ω] .* im * ϵ₀ * Πs^2\n",
    "end\n",
    "fϵ(ω, n) = I(3) + im * (fσ(ω, n, mₑ, -1) + fσ(ω, n, mi, 1)) / (ω * ϵ₀)\n",
    "fkvector(k) = k * [sin(θ₀), 0, cos(θ₀)]\n",
    "fΛ(ω, k::Number, n) = fΛ(ω, fkvector(k), n)\n",
    "function fΛ(ω, k, n)\n",
    "    N = k * c₀ / ω\n",
    "    output = N * N' .- dot(N, N) * I(3) .+ fϵ(ω, n)\n",
    "    # @show abs(det(output))\n",
    "    return output\n",
    "end\n",
    "function fE(ω, k, n)\n",
    "  Λ = fΛ(ω, k, n)\n",
    "  _, sigma, V = svd(Λ)\n",
    "  @assert sigma[3] <= sqrt(eps()) \"sigma[3] = $(sigma[3]), det(Λ) = $(det(Λ))\"\n",
    "  E = V[:, 3]\n",
    "  @assert all(E .≈ nullspace(Λ))\n",
    "  @assert all(abs.(Λ * E) .<= sqrt(eps()))\n",
    "  return E\n",
    "end\n",
    "absdetΛ(ω, k, n=n0) = abs(det(fΛ(ω, k, n)))\n",
    "\n",
    "fB(ω, k, n) = cross(fkvector(k), fE(ω, k, n)) / ω\n",
    "fJ(ω, k, n, m::Float64, Z::Int) = fσ(ω, n, m, Z) * fE(ω, k, n)\n",
    "fJ(ω, k, n) = fJ(ω, k, n, mₑ, -1) + fJ(ω, k, n, mi, 1)\n",
    "fV(ω, k, n, m::Float64=mₑ, Z::Int=-1) = fJ(ω, k, n, m, Z) / q₀ / n / Z\n",
    "fn(ω, k, n, m::Float64=mₑ, Z::Int=-1) = n * dot(fkvector(k), fV(ω, k, n, m, Z)) / ω\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "74890d47",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "solvefork (generic function with 2 methods)"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# vthi = sqrt(Te_eV * q₀ * 2 / mi)\n",
    "# rli = vthi / Ωi\n",
    "# vthe = sqrt(Te_eV * q₀ * 2 / mₑ)\n",
    "# rle = vthe / abs(Ωe)\n",
    "function solvefork(ω = 140e9 * 2π)\n",
    "  k = Roots.fzero(k->absdetΛ(ω, k, n0), ω / c₀, fatol=eps())\n",
    "  @show k\n",
    "  @show ω / k / c₀\n",
    "  @show ω / fΠ(n0, mₑ)\n",
    "  @show ω / k / vthe\n",
    "  (k⊥, _, kz) = fkvector(k)\n",
    "  return k\n",
    "end\n",
    "\n",
    "# Lx = 2π / k * 10"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "be9ae429",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "findpolarisation (generic function with 1 method)"
      ]
     },
     "execution_count": 3,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "permuterkzk⊥_kxkykz(x) = [x[3], x[1], x[2]]\n",
    "\n",
    "function checkpolarisation(ω, k, polarisation)\n",
    "  E = polarisation[:E]\n",
    "  B = polarisation[:B]\n",
    "  J = polarisation[:J]\n",
    "  ve = polarisation[:ve]\n",
    "  vi = polarisation[:vi]\n",
    "  ne = polarisation[:ne]\n",
    "  ni = polarisation[:ni]\n",
    "  kvec = polarisation[:kvec]\n",
    "  @assert abs(dot(kvec, B)) < norm(k) * norm(B) * 1e-8 # Monopoles\n",
    "  @assert all(ω * B ≈ cross(kvec, E)) # Faraday\n",
    "  @assert isapprox(-im * J, cross(kvec, B) / μ₀ + ω * ϵ₀ * E, rtol=1e-6) # Ampere\n",
    "  @assert isapprox(im * dot(kvec, E) * ϵ₀, q₀ * (ni - ne), rtol=1e-6) # Gauss\n",
    "  @assert isapprox(im * dot(kvec, J), im * q₀ * (ni - ne) * ω, rtol=1e-6) # divJ + drho/dt = 0 \n",
    "  @assert all(J ≈ q₀ * n0 * (vi - ve)) # current\n",
    "end\n",
    "\n",
    "function findpolarisation(ω, k, n)\n",
    "  Ep = permuterkzk⊥_kxkykz(cleaner(fE(ω, k, n)))\n",
    "  Bp = permuterkzk⊥_kxkykz(cleaner(fB(ω, k, n)))\n",
    "  Jp = permuterkzk⊥_kxkykz(cleaner(fJ(ω, k, n)))\n",
    "  ve = permuterkzk⊥_kxkykz(cleaner(fV(ω, k, n, mₑ, -1)))\n",
    "  vi = permuterkzk⊥_kxkykz(cleaner(fV(ω, k, n, mi, 1)))\n",
    "  ne = cleaner(fn(ω, k, n, mₑ, -1))\n",
    "  ni = cleaner(fn(ω, k, n, mi, 1))\n",
    "  kvec = permuterkzk⊥_kxkykz(fkvector(k))\n",
    "  pol = (E=Ep, B=Bp, J=Jp, ve=ve, vi=vi, ne=ne, ni=ni, kvec=kvec)\n",
    "  checkpolarisation(ω, k, pol)\n",
    "  return pol\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "5cc76aa9",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "printer (generic function with 1 method)"
      ]
     },
     "execution_count": 4,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "const ignorekeys = [:kvec, :ω]\n",
    "function scale(polarisation, factor::Real)\n",
    "  return NamedTuple(k=> k in ignorekeys ? v : factor * v for (k, v) in pairs(polarisation))\n",
    "end\n",
    "function zeroions(polarisation)\n",
    "  return NamedTuple(k=>!(k in (:vi, :ni)) * v for (k, v) in pairs(polarisation))\n",
    "end\n",
    "function energy(polarisation)\n",
    "  E = polarisation[:E]\n",
    "  B = polarisation[:B]\n",
    "  ve = polarisation[:ve]\n",
    "  vi = polarisation[:vi]\n",
    "  return real(ϵ₀ * dot(E,E) / 2 + dot(B,B) / 2μ₀ +\n",
    "            mₑ * n0 * dot(ve,ve) / 2 + mi * n0 * dot(vi,vi) / 2)\n",
    "end\n",
    "printer(polarisation, i) = printer(stdout, polarisation, i)\n",
    "\n",
    "function printcommon(io, pol)\n",
    "  kx, ky, kz = pol[:kvec]\n",
    "  λx = 2π / kx\n",
    "  λy = 2π / ky\n",
    "  println(io, \"n0 = $n0\")\n",
    "  println(io, \"B0 = $B0\")\n",
    "  println(io, \"lambdadebye = $λD0\")\n",
    "  cellx = min(λD0, λx / 2)\n",
    "  celly = min(λD0, λy / 2)\n",
    "  Lx = ceil(0.2 / λx) * λx\n",
    "  Ly = ceil(0.2 / λy) * λy\n",
    "  NGX = ceil(Int, Lx / cellx)\n",
    "  NGY = ceil(Int, Ly / celly)\n",
    "  # println(io, \"Lx = $Lx\")\n",
    "  println(io, \"NGX = $NGX\")\n",
    "  println(io, \"NGY = $NGY\")\n",
    "  println(io, \"Te_eV = $Te_eV\")\n",
    "  println(io, \"Ti_eV = $Ti_eV\")\n",
    "  println(io, \"mioverme = $(round(Int, mi / mₑ))\")\n",
    "  # println(io, \"waveenergytobulkmagneticfieldenergyratio = $energyratio\")\n",
    "end\n",
    "function printscalar(io, scalar, str)\n",
    "  println(io, str * \"_cos = $(real(scalar))\")\n",
    "  # Minus sign because real((a + im*b) * exp(im *k*x)) = a*cos(k*1x) - b*sin(k*x).\n",
    "  println(io, str * \"_sin = $(-imag(scalar))\")\n",
    "end\n",
    "function printvector(io, vec, str)\n",
    "  components = [\"x\", \"y\", \"z\"]\n",
    "  for i in 1:3\n",
    "    printscalar(io, vec[i], str * components[i])\n",
    "  end\n",
    "end\n",
    "\n",
    "function printer(io, polarisation)\n",
    "  printvector(io, polarisation[:E], \"E\")\n",
    "  printvector(io, polarisation[:B], \"B\")\n",
    "  printvector(io, polarisation[:J], \"J\")\n",
    "  printvector(io, polarisation[:ve] * mₑ, \"Pe\")\n",
    "  printvector(io, polarisation[:vi] * mi, \"Pi\")\n",
    "  printscalar(io, polarisation[:ne], \"ne\")\n",
    "  printscalar(io, polarisation[:ni], \"ni\")\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "2db93f95-8aea-446d-bc0e-358221d7591d",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "k = 11.324627367918831\n",
      "(ω / k) / c₀ = 0.28216565113086745\n",
      "ω / fΠ(n0, mₑ) = 0.016980764298206927\n",
      "(ω / k) / vthe = 1.4262613950373726\n",
      "norm(pol[:E]) = 9999.999999999998\n",
      "pol = (E = ComplexF64[14.150318993634999 - 0.0im, 7946.594028932778 - 0.0im, -1.1999999999999997e-10 + 6070.538947062465im], B = ComplexF64[-9.038300184365229e-19 + 4.6128560162753696e-5im, 1.086627100816943e-18 - 5.497387729515988e-5im, 7.18556218852791e-5 + 0.0im], J = ComplexF64[0.0 + 416.3585783096637im, 9.69958601127517e-12 - 428.6509028323116im, 595.2300285130685 - 9.27786488035016e-13im], ve = ComplexF64[-0.0 - 2597.998567231426im, -5.985150637281672e-11 + 3046.112590437706im, -3981.5925280569063 - 0.0im], vi = ComplexF64[0.0 + 0.7075159496817557im, 5.93471257186084e-13 + 370.6838451629561im, -266.4585628368276 - 6.026015842197161e-12im], ne = -0.4566387926577154 - 3.804055253729718e11im, ni = 0.0045170307539182985 + 2.8231442211989365e12im, kvec = [8.67516786558732, 7.27933015641531, 0.0])\n"
     ]
    },
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "\u001b[36m\u001b[1m[ \u001b[22m\u001b[39m\u001b[36m\u001b[1mInfo: \u001b[22m\u001b[39mdecks written at 2024-10-04T12:01:13.614\n"
     ]
    }
   ],
   "source": [
    "ω = 10Ωi #140e9 * 2π\n",
    "k = solvefork(ω)\n",
    "pol = findpolarisation(ω, k, n0)\n",
    "@assert norm(pol[:E]) ≈ 1 norm(pol[:E])# by convention\n",
    "\n",
    "bfieldenergydensity = fbulkmagneticfieldenergydensity(n0)\n",
    "# energyratio = 1e-4\n",
    "# checkpolarisation(ω, k, pol) # make sure they're still consitent\n",
    "# @show sqrt(bfieldenergydensity * energyratio / energy(pol))\n",
    "# energyscale = sqrt(bfieldenergydensity * energyratio / energy(pol)) # how to scale energy\n",
    "pol = scale(pol, 1e4)\n",
    "@show norm(pol[:E])\n",
    "# @assert energy(pol) ≈ energyratio * bfieldenergydensity\n",
    "checkpolarisation(ω, k, pol) # make sure they're still consitent\n",
    "kx, ky, _ = pol[:kvec]\n",
    "@show pol\n",
    "\n",
    "open(\"null.prefix.deck\",\"w\") do io\n",
    "  println(io, \"begin:constant\")\n",
    "  printcommon(io, pol)\n",
    "  println(io, \"# Wave\")\n",
    "  println(io, \"w = $ω\")\n",
    "  println(io, \"k = $k\")\n",
    "  println(io, \"kx = $kx\")\n",
    "  println(io, \"ky = $ky\")\n",
    "  printer(io, scale(pol, 0))\n",
    "end\n",
    "\n",
    "open(\"wave.prefix.deck\",\"w\") do io\n",
    "  println(io, \"begin:constant\")\n",
    "  printcommon(io, pol)\n",
    "  println(io, \"# Wave\")\n",
    "  println(io, \"w = $ω\")\n",
    "  println(io, \"kx = $kx\")\n",
    "  println(io, \"ky = $ky\")\n",
    "  printer(io, pol)\n",
    "end\n",
    "\n",
    "@info \"decks written at $(now())\""
   ]
  }
 ],
 "metadata": {
  "@webio": {
   "lastCommId": "3eaf98e13948451e97f316806ca6c0be",
   "lastKernelId": "8dc15439-d5a9-4d67-87ba-cd06ec2f286f"
  },
  "kernelspec": {
   "display_name": "Julia 1.10.5",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
